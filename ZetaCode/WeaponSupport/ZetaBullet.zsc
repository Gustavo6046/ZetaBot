// 'Projectile' for bullet-based ZetaWeapons

class ZetaBullet : Actor
{
	String bulletColor;
	Property BulletColor: bulletColor;

	Default
	{
		Damage 0;
		Speed 80; // TO-DO: a more reasonable speed for a bullet while avoiding FastProjectile
		ZetaBullet.BulletColor "Gold";
		Species "ZetaBot";
		+THRUSPECIES
		Projectile;
	}
	
	void SetColor(String col)
	{
		bulletColor = col;
	}

	static ZetaBullet FireABullet(Actor shooter, String bulletColor, Actor target, double damage, double spreadX, double spreadY)
	{
		let bullet = shooter.SpawnMissile(target, "ZetaBullet", shooter);
		
		if ( bullet != null )
		{
			bullet.SetDamage(Floor(damage + 0.5));
			bullet.angle += FRandom(-spreadX, spreadX);
			bullet.pitch += FRandom(-spreadY, spreadY);
			ZetaBullet(bullet).SetColor(bulletColor ? bulletColor : "Gold");
		}
			
		return ZetaBullet(bullet);
	}

	static Array<ZetaBullet> FireBullets(Actor shooter, String bulletColor, Actor target, double damage, int numBullets, double spreadX, double spreadY)
	{
		Array<ZetaBullet> res;
	
		for ( int i = 0; i < numBullets; i++ )
			res.Push(FireABullet(shooter, bulletColor, target, damage, spreadX, spreadY));
			
		return res;
	}
	
	States
	{
		Spawn:
			TNT1 A 1
			{
				double pos;
			
				for ( pos = 0; pos > -sqrt(vel.x * vel.x + vel.y * vel.y + vel.z * vel.z); pos -= 2 )
					A_SpawnParticle(bulletColor, SPF_FULLBRIGHT | SPF_RELPOS | SPF_RELANG, 15, 1, 0, pos, 0, vel.z * pos, FRandom(-0.01 * pos, 0.01 * pos), FRandom(-0.01 * pos, 0.01 * pos), FRandom(-0.01 * pos, 0.01 * pos));
			}
			Loop;
			
		Death:
			TNT1 A 0
			{
				if ( FRandom(0, 99.9) < 35 )
					A_PlaySound("ztmisc/ricochet", CHAN_BODY, FRandom(0.2, 0.7));
			}
			Stop;
			
		XDeath:
			TNT1 A 0 A_SpawnItemEx("Blood");
			Stop;
	}
}